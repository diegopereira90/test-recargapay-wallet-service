plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'jacoco'
}

group = 'br.com.recargapay'
version = '0.0.1-SNAPSHOT'
description = 'Microservice for wallets'

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        force 'net.logstash.logback:logstash-logback-encoder:7.4'
        force 'io.opentelemetry:opentelemetry-api:1.49.0'
        force 'io.opentelemetry:opentelemetry-sdk:1.49.0'
        force 'io.opentelemetry:opentelemetry-context:1.49.0'
        force 'io.opentelemetry:opentelemetry-exporter-otlp:1.49.0'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.11'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    implementation 'io.opentelemetry:opentelemetry-api:1.49.0'
    implementation 'io.opentelemetry:opentelemetry-sdk:1.49.0'
    implementation 'io.opentelemetry:opentelemetry-context:1.49.0'
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp:1.49.0'
    implementation 'io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter:2.0.0-alpha'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    runtimeOnly 'com.h2database:h2'
    implementation 'org.postgresql:postgresql:42.6.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
}

checkstyle {
    toolVersion = '10.17.0'
    configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
}

jacoco {
    toolVersion = "0.8.10"
}

tasks.test {
    useJUnitPlatform()
    finalizedBy tasks.jacocoTestReport
}

tasks.jacocoTestReport {
    dependsOn tasks.test
    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.required.set(true)
        html.outputLocation.set(layout.buildDirectory.dir("reports/jacoco/html"))
    }
    classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/*Application.class',
                        '**/*Entity.class',
                        '**/AuditableEntity.class'
                ])
            })
    )
}
